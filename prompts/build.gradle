import groovy.io.FileType
import groovy.json.JsonSlurper

sourceSets {
    text
    data
}

task generatePromptList {
    inputs.files processTextResources
    def texFile = file("$sourceSets.text.output.resourcesDir/prompts.tex")
    outputs.files texFile
    doLast {
        texFile.withWriter { tex ->
            tex.println "\\documentclass[aspectratio=169]{beamer}"
            tex.println "\\usepackage[utf8]{inputenc}"
            tex.println "\\beamertemplatenavigationsymbolsempty"
            tex.println "\\setbeamertemplate{footline}[frame number]"
            tex.println "\\newcommand*{\\prompt}[1]{%"
            tex.println "  \\frame{\\Huge\\centering #1}"
            tex.println "}"
            tex.println "\\begin{document}"
            inputs.files.each { dir ->
                dir.eachFile { file ->
                    file.eachLine { line ->
                        tex.println "\\prompt{$line}"
                    }
                }
            }
            tex.println "\\end{document}"
        }
    }
}

task compilePromptList(type: Exec) {
    inputs.files generatePromptList
    outputs.files "$buildDir/prompts.pdf"
    executable 'latexmk'
    doFirst {
        args '-pdf', "-output-directory=$temporaryDir", '-quiet', inputs.files.singleFile
    }
    doLast {
        copy {
            from temporaryDir
            into buildDir
            include 'prompts.pdf'
        }
    }
}

task convertTextGridsToJson {
    def textGridFiles = fileTree(sourceSets.data.output.resourcesDir).include('*.TextGrid').files
    def praatScript = 'src/data/praat/TextGrid2JSON.praat'
    inputs.files processDataResources, praatScript
    outputs.files textGridFiles.collect { it.path - 'TextGrid' + 'json' }
    doLast {
        textGridFiles.each { textGridFile ->
            def jsonFileName = textGridFile.path - 'TextGrid' + 'json'
            exec {
                commandLine "praat $praatScript $textGridFile $jsonFileName".tokenize()
            }
        }
    }
}

task extractAudio {
    inputs.files processDataResources

    doLast {
        inputs.files.each { dir ->
            dir.eachFile { jsonFile ->
                if (jsonFile.name.endsWith('.json')) {
                    def waveFile = file(jsonFile.path.substring(0,jsonFile.path.length()-'.json'.length()) + '.wav')
                    if(waveFile.exists()) {
                        new JsonSlurper().parse(jsonFile).each { prompt ->
                            mkdir "$sourceSets.data.output.resourcesDir/extracted"
                            "sox $waveFile.path $sourceSets.data.output.resourcesDir/extracted/${prompt.prompt}.wav trim $prompt.start =$prompt.end".execute()
                            file("$sourceSets.data.output.resourcesDir/extracted/${prompt.prompt}.txt").text = prompt.text
                        }
                    }
                }
            }
        }

    }
}